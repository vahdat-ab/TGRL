grammar org.xtext.grl.tgrl.TGRL with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//import "http:///ucm.ecore" as ucmModel
//import "http:///urncore.ecore" as urncoreModel
//import "http:///urn.ecore" as urnModel
//import "http:///asd.ecore" as asdModel
//import "http:///grl.ecore" as grlModel

generate tGRL "http://www.xtext.org/grl/tgrl/TGRL"

Model:
	gRLspecs+=GRLspec*;

GRLModelElement:
	GRLNode | GRLLinkableElement | ElementLink | StrategiesGroup | EvaluationStrategy | 
	ContributionContextGroup | ContributionContext | KPIConversion | KPIInformationElement | KPIModelLink | IndicatorGroup 
;
GRLNode:
	Belief | CollapsedActorRef | IntentionalElementRef | KPIInformationElementRef
;
GRLLinkableElement:
	IntentionalElement | Actor
;	
IntentionalElement:
	 Softgoal | Goal  | Task | Ressource | Indicator
;
ElementLink: 
	Decomposition | Contribution | Dependency
;
KPIConversion:
	QualitativeMappings
;

GRLspec: 
	'grl' '{'
		(
			(impactModel=ImpactModel)?
			(
				links+=ElementLink | actors+=Actor | intElements+=IntentionalElement | groups+=StrategiesGroup | 
				strategies+=EvaluationStrategy | contributionGroups+=ContributionContextGroup |
				contributionContexts+=ContributionContext | kpiConv+=KPIConversion	| kpiInformationElements+=KPIInformationElement |
				kpiModelLinks+=KPIModelLink | indicatorGroup+=IndicatorGroup
			)*	
		)
	'}'	
;


Actor: 
	'actor' name= ID '{'
		(
		('name' '=' label =STRING ';')?&	
		('importance' '=' importance=ImportanceType ';')?&
		/*
		 *TODO I must give a default value, which is zero, to this attribute.
		 * 		This can be done through the post processing mechanism in xText 
		 */
		('importanceQuantitative' '=' importanceQuantitative = INT ';')?&
		('description' '=' description=STRING ';')?&
		('includedActors' '=' includedActors+=[Actor](',' includedActors+=[Actor])* ';')?&		
		('collapsedRefs' '=' collapsedRefs+=[CollapsedActorRef](',' collapsedRefs+=[CollapsedActorRef])* ';')?
//		('contRefs' '=' contRefs+=[urncoreModel::IURNContainerRef](',' contRefs+=[urncoreModel::IURNContainerRef])* ';')?
		)
		
	'}'
;


//*************************************IntentionalElement******************************************
Softgoal :
	'softGoal' name = ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('importance' '=' importance=ImportanceType ';')?&
			('decompositionType' '=' decompositionType=DecompositionType ';')?&
			/*
			 *TODO I must give a default value, which is zero, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */
			('importanceQuantitative' '=' importanceQuantitative=INT ';')?&
			('refs' '=' refs+=[IntentionalElementRef](',' refs+=[IntentionalElementRef] )* ';')?
			
		)
	'}'
;
Goal:
	'goal' name = ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('importance' '=' importance=ImportanceType ';')?&
			('decompositionType' '='decompositionType=DecompositionType ';')?&
			/*
			 *TODO I must give a default value, which is zero, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */
			('importanceQuantitative' '=' importanceQuantitative=INT ';')?&
			('refs' '=' refs+=[IntentionalElementRef](',' refs+=[IntentionalElementRef] )* ';')?&
			('evals' '=' evals+=[Evaluation](',' evals+=[Evaluation] )* ';')?
			
		)		
	'}'
; 
Task:
	'Task' name = ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('importance' '=' importance=ImportanceType ';')?&
			('decompositionType' '='decompositionType=DecompositionType ';')?&
			/*
			 *TODO I must give a default value, which is zero, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */
			('importanceQuantitative' '=' importanceQuantitative=INT ';')?&
			('refs' '=' refs+=[IntentionalElementRef](',' refs+=[IntentionalElementRef] )* ';')?&
			('evals' '=' evals+=[Evaluation](',' evals+=[Evaluation] )* ';')?
			 
		)		
	'}'
;  
Ressource:
	'resource' name = ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('importance' '=' importance=ImportanceType ';')?&
			('decompositionType' '='decompositionType=DecompositionType ';')?&
			/*
			 *TODO I must give a default value, which is zero, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */
			('importanceQuantitative' '=' importanceQuantitative=INT ';')?&
			('refs' '=' refs+=[IntentionalElementRef](',' refs+=[IntentionalElementRef] )* ';')?			
		)		
	'}'
; 
Indicator:
	'indicator' name = ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('importance' '=' importance=ImportanceType ';')?&
			('decompositionType' '='decompositionType=DecompositionType ';')?&
			/*
			 *TODO I must give a default value, which is zero, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */
			('importanceQuantitative' '=' importanceQuantitative=INT ';')?&
			('refs' '=' refs+=[IntentionalElementRef](',' refs+=[IntentionalElementRef] )* ';')?&
			('kpiModelLinksDest' '=' kpiModelLinksDest+=[KPIModelLink](',' kpiModelLinksDest+=[KPIModelLink] )* ';')?&
			('groups''=' groups+=[IndicatorGroup](',' groups+=[IndicatorGroup] )* ';')?			
		)		
	'}'
;  
//*******************************************ElementLink*******************************************
Decomposition: 
	'decomposition' (name=ID)? '{'	
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
	//		('refs' '=' refs+=[LinkRef] (',' refs+=[LinkRef])* ';')?
			(src=[GRLLinkableElement] '->' dest=[GRLLinkableElement] ';')
		)
	'}'
;
Contribution: 
	'contribution' (name=ID)? '{'
		(
			('name' '=' label = STRING ';')?&
			('contribution' '=' contribution=ContributionType ';')?&
			/*
			 *TODO I must give a default value, which is 25, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */
			('quantitativeContribution' '=' quantitativeContribution=INT ';')?&
			('correlation' '=' correlation=BOOLEAN ';')?&
			('description' '=' description=STRING ';')?&
			('changes' '=' changes+=[ContributionChange](',' changes+=[ContributionChange])* ';')?&
	//		('refs' '=' refs+=[LinkRef] (',' refs+=[LinkRef])* ';')?&
			(src=[GRLLinkableElement] '->' dest=[GRLLinkableElement] ';')					
		)
	'}'
;
Dependency:
	'dependency' (name=ID)? '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
	//		('refs' '=' refs+=[grlModel::LinkRef] (',' refs+=[grlModel::LinkRef])* ';')?&
			(src=[GRLLinkableElement] '->' dest=[GRLLinkableElement] ';')
		)
	'}' 
;
//**************************************************************************************
Belief: 
	'belief' name=ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' description=STRING ';')?
		)		
	'}'
;	

CollapsedActorRef: 
	'collapsedActorRef' name=ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' description=STRING ';')?	
		)
	'}'
;
IntentionalElementRef : 
	'IntentionalElementRef' name=ID '{'
		(
			('criticality' '=' criticality=Criticality ';')?&
			('priority' '=' priority=Priority';')?&
			('def' '=' def=[IntentionalElement] ';')?					
		)
	'}'
;
ImpactModel :
	'impactModel' name= ID '{'
	'}'
;

//**************************************************************************************
StrategiesGroup: 
	'strategiesGroup' name=ID '{'
		('strategies' '=' strategies+=[EvaluationStrategy](',' strategies+=[EvaluationStrategy])* ';')*
	'}'
;
EvaluationStrategy: 
	'EvaluationStrategy' name=ID '{'
		(
			(
				('evaluations' '=' evaluations+=Evaluation';') | 
				('includedStrategies' '=' includedStrategies+=[EvaluationStrategy](',' includedStrategies+=[EvaluationStrategy])* ';') |
				('kipInforConfig' '='kipInforConfig+=KPIInformationConfig ';')
			)*
		)
	'}'
;
Evaluation:
	'eval' name = ID '{'
		(
			/*
			 *TODO I must give a default value, which is zero, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('evaluation' '=' evaluation = INT ';')?&
			('qualitativeEvaluation' '=' qualitativeEvaluation=QualitativeLabel ';')?&
			('exceeds' '=' exceeds = BOOLEAN)?&
			('evalRange' '=' evalRange=EvaluationRange ';')?&
			('kpiEvalValueSet' '=' kpiEvalValueSet = KPIEvalValueSet ';')?&
			('kpiNewEvalValue' '=' kpiNewEvalValue = KPINewEvalValue)?&
			('inElement' '=' inElement=[IntentionalElement] ';') 
		) 
	'}'
;
EvaluationRange: 
	'evaluationRange' '{'
		(
			('start' '=' start=INT ';')?&
			('end' '=' end=INT ';')?&
			/*
			 *TODO I must give a default value, which is 1, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('step' '=' step=INT ';')?
		)
	'}'
;
ContributionContextGroup: 
	'contributionContextGroup' name=ID '{'
		('contribs' '=' contribs+=[ContributionContext](',' contribs+=[ContributionContext])* ';')
	'}'
;
ContributionContext: 
	'contributionContext' name= ID '{'
		(
			('includedContexts' '=' includedContexts+=[ContributionContext](',' includedContexts+=[ContributionContext])* ';')?&
			('changes' '=' changes+=ContributionChange)*
		)
	'}'
;
ContributionChange:
	'contributionChange' name=ID '{'
		(
			('newContribution' '=' newContribution=ContributionType ';')?&
			/*
			 *TODO I must give a default value, which is 0, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('newQuantitativeContribution' '=' newQuantitativeContribution = INT ';')?&
			('contribRange' '=' contribRange = ContributionRange ';')?
		)
	'}'
;
ContributionRange: 
	'contributionRange' '{'
		(
			('start' '=' start=INT ';')?&
			('end' '=' end=INT ';')?&
			/*
			 *TODO I must give a default value, which is 1, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('step' '=' step=INT ';')?
		)
	'}'
;
QualitativeMappings:
	'qualitativeMappings' name=ID '{'
		(
			('kpiEvalValueSet' '=' kpiEvalValueSet+=[KPIEvalValueSet](',' kpiEvalValueSet+=[KPIEvalValueSet])* ';')*&	
			(mappin+=QualitativeMapping)*	
		)
		
	'}'
;

KPIEvalValueSet:
	'kpiEvalValueSet' name=ID '{'
		(
			/*
			 *TODO I must give a default value, which is 0, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('targetValue' '=' targetValue=DOUBLE ';')?&
			/*
			 *TODO I must give a default value, which is 0, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('thresholdValue' '=' thresholdValue=DOUBLE ';')?&
			/*
			 *TODO I must give a default value, which is 0, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('worstValue' '=' worstValue=DOUBLE ';')?&
			/*
			 *TODO I must give a default value, which is 0, to this attribute.
			 * 		This can be done through the post processing mechanism in xText 
			 */	
			('evaluationValue' '=' evaluationValue=DOUBLE ';')?&	
			('unit' '=' unit=STRING ';')?&
			('qualitativeEvaluationValue' '=' qualitativeEvaluationValue=STRING ';')?						
		)
	'}'
;
QualitativeMapping:

	'qualitativeMapping' name=ID '{'
		(
			('realWorldLabel' '=' realWorldLabel=STRING ';')?&
			('evaluation' '=' evaluation=INT ';')?&
			('qualitativeEvaluation' '=' qualitativeEvaluation=QualitativeLabel ';')?&
			('exceeds' '=' exceeds=BOOLEAN ';')?				
		)
	'}'
;
KPINewEvalValue:
	'kpiNewEvalValue' name=ID '{'
		/*
		 *TODO I must give a default value, which is 0, to this attribute.
		 * 		This can be done through the post processing mechanism in xText 
		 */	
		'evaluationValue' '=' evaluationValue=DOUBLE ';'
	'}'
;
KPIInformationConfig:
	'kpiInformationConfig' name=ID '{'
		(
			('levelOfDimension' '=' levelOfDimension = STRING ';')?&
			('valueOfDimension' '=' valueOfDimension=STRING ';')?&
			('kpiInfoElement' '=' kpiInfoElement=[KPIInformationElement])
		)
	'}'
;
KPIInformationElement:
	'kpiInformationElement' name=ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('kpiModelLinksSrc' '=' kpiModelLinksSrc+=[KPIModelLink](',' kpiModelLinksSrc+=[KPIModelLink] )* ';')?&
			('refs' '=' refs+=[KPIInformationElementRef](',' refs+=[KPIInformationElementRef] )* ';')?
		)
	'}'
;
KPIInformationElementRef:
	'kpiInformationElementRef' name=ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?
		)
	'}'
;
KPIModelLink:
	'kpiModelLink' name=ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?
		)
	'}'
;
IndicatorGroup :
		'indicatorGroup' name=ID '{'
		(
			('name' '=' label = STRING ';')?&
			('description' '=' description=STRING ';')?&
			('isRedesignCategory' '=' isRedesignCategory=BOOLEAN ';')?&
			('indicators''=' indicators+=[Indicator](',' indicators+=[Indicator] )* ';')?
		)
	'}'
;
//*******1*******************************************************************************


//
//URNspec returns urnModel::URNspec: 
//	'urnspec' name= ID '{'
//		(
//		(urndef=URNdefinition)&
//		(grlspec=GRLspec)?&
//		(metadata+=Metadata*)&
//		(ucmspec=UCMspec)?&
//		(urnLinks+=URNlink*)&
//		(asdspec=ASDspec)
//		)
//	'}'
//	
//;
//
//URNdefinition returns urncoreModel::URNdefinition: 
//	'URNdefinition' '{'
//		{urncoreModel::URNdefinition}
//	'}'
//;
//ASDspec returns asdModel::ASDspec: 
//	'asdspec'  '{'
//		{asdModel::ASDspec}		
//	'}' 
//;
//URNlink returns urnModel::URNlink: 
//	'urnLink' '{'
//	'}'
//;
//UCMspec returns ucmModel::UCMspec: 
//	'ucmSpec' '{'
//		
//	'}'
//;
//
//




//Metadata returns urncoreModel::Metadata: 
//	'metaData' name=ID '{'
//		(
//		('value' '=' value=STRING ';')?
//		)
//	'}'
//;

//enum IntentionalElementType:
//	Softgoal = 'softGoal' | Goal = 'goal' | Task = 'Task' | 
//	Ressource = 'resource'| Indicator = 'indicator'  
//;
enum ImportanceType: 
	 None  | High  | Medium | Low
;
enum DecompositionType:
	And| Or  | Xor  
;

enum ContributionType: 
	Help | Make | SomePositive  |
	Unknown  | SomeNegative  | Hurt  |
	Break 
;

enum QualitativeLabel:
	Denied | WeaklyDenied |
	WeaklySatisfied  | Satisfied |
	Conflict | Unknown | None
;

//Deprecated
enum Criticality:
	None | High | Medium | Low  
;
//Deprecated
enum Priority:
	None | High  | Medium  | Low 
;
//Terminals
terminal BOOLEAN:
'false' | 'true'
;
terminal fragment DIGIT: '0'..'9';
terminal fragment HEX_DIGIT: (DIGIT |'a'..'f'|'A'..'F');
terminal fragment ALPHANUM: ('a'..'z'|'A'..'Z'|'_'|DIGIT);
terminal HEXINT: '0' ('x'|'X') HEX_DIGIT+;
terminal OCTINT: '0' ('0'..'7')+;
terminal DOUBLE returns ecore::EDouble: DECINT (('.' DIGIT*) | (('.' DIGIT*)? ('E'|'e') ('-'|'+')? DECINT));  // Use terminal to avoid 'e' turning into a keyword
terminal DECINT: '0' | ('1'..'9' DIGIT*);
